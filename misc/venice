#!/usr/bin/env perl
#
# Runs a command with specified priority, like nice, but handles
# negative priorities with sudo. Forks to avoid running the named
# command as superuser; renice done via parent process as root.
#
# Example: venice open -a D2X
#
# On Mac OS X launches "Descent2 X" and renices it to the default
# priority.

use strict;
use warnings;

my $default_nice = -10;
my $nice_value   = $default_nice;

use File::Basename qw(basename);
my $command = basename $0;

# need custom argument processing as standard Getopt Std/Long have
# issues easily handling '-n +/-20' followed by the command to run
while ( my $arg = shift @ARGV ) {
  if ( $arg eq '-?' or $arg =~ m/-h/ ) {
    print_help();
  } elsif ( $arg eq '-n' ) {
    $nice_value = shift @ARGV;

    print_help()
      if $nice_value !~ m/^[+-]?\d\d?$/
      or $nice_value > 20
      or $nice_value < -20;

    next;
  } elsif ( $arg eq '--' ) {
    last;
  }

  unshift @ARGV, $arg;
  last;
}

print_help() unless @ARGV;

# positive priorities require no special handling
if ( $nice_value >= 0 ) {
  exec qw{nice -n}, $nice_value, @ARGV;
  warn "error: could not exec command: errno=$!\n";
}

# For negative priorities, need to fork and sudo. Validate with sudo
# prior to this, to avoid stalling after forked on a password prompt.
system(qw{sudo -v}) == 0 or die "error: problem with sudo -v: $?\n";

if ( my $child_pid = fork ) {

  # renice child process in parent
  exec qw{sudo renice}, $nice_value, '-p', $child_pid;
  die "error: could not renice: errno=$!\n";

} else {

  # replace child with named command
  exec @ARGV;
  warn "error: could not exec command: errno=$!\n";
}

sub print_help {
  warn "\nUsage: $command [-n increment] utility [argument ...]\n\n";
  warn "  increment: -20 to 20, negative is higher priority\n";
  warn "  utility:   Command to run with optional arguments\n\n";
  exit 64;
}
