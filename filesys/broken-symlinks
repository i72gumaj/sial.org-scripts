#!/usr/bin/env perl
#
# Reports dangling or self-pointing symlinks. Use the --exclude option
# as often as necessary to specify regular expressions of things to
# ignore, for example files under .mozilla. Use -0 to null separate the
# output (path to symlink, dangle|self, readlink of symlink). (However,
# filenames with newlines in them would be problematic, and would
# perhaps require nulls everywhere, and then reading by sets of three
# elements, or some other solution.)

use strict;
use warnings;

use File::Find qw/find/;
use Getopt::Long qw/GetOptions/;

my $Record_Sep = ' ';
GetOptions( '0' => \my $Flag_Nullsep, 'exclude=s' => \my @Excludes )
  or die "Usage: [-0] [--exclude=.mozilla] $0 dir [dir2 ..]\n";
$Record_Sep = "\0" if $Flag_Nullsep;

@ARGV = grep -d, @ARGV;
die "Usage: [-0] [--exclude=.mozilla] $0 dir [dir2 ..]\n" unless @ARGV;

find( { wanted => \&handle_symlinks, follow => 0 }, @ARGV );

sub handle_symlinks {
  return unless -l;

  my $where = readlink;
  return if -e $where;    # exists, do not care

  for my $e (@Excludes) {
    return if $File::Find::name =~ m{$e};
  }

  my $reason = 'dangle';
  $reason = 'self' if $where eq $File::Find::name or $where eq $_;

  print join( $Record_Sep, $File::Find::name, $reason, $where ), "\n";
}
