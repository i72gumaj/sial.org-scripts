#!/usr/bin/env perl
#
# Somewhat improve the horrible /proc/net/rpc/nfsd "tr " output. Shows the
# delta from the previous value instead of unreadable counters, "." if no
# change in that column.

use strict;
use warnings;

my $sleep_secs = shift || 60;
my @prev;

while (1) {
  open my $fh, '<', "/proc/net/rpc/nfsd"
    or die "could not open /proc/net/rpc/nfsd: $!\n";
  while ( readline $fh ) {
    if (m/^th /) {
      my @data = split;
      splice @data, 0, 2;
      if (@prev) {
        my @delta;
        for my $i ( 0 .. $#data ) {
          push @delta, sprintf "%.1f", $data[$i] - $prev[$i];
        }
        print join( "\t", map { $_ == 0 ? '.' : $_ } @delta ), "\n";
      }
      @prev = @data;
      last;
    }
  }
  sleep $sleep_secs;
}
